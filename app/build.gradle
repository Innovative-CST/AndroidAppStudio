/*
 * This file is part of Android AppStudio [https://github.com/TS-Code-Editor/AndroidAppStudio].
 *
 * License Agreement
 * This software is licensed under the terms and conditions outlined below. By accessing, copying, modifying, or using this software in any way, you agree to abide by these terms.
 *
 * 1. **  Copy and Modification Restrictions  **
 *    - You are not permitted to copy or modify the source code of this software without the permission of the owner, which may be granted publicly on GitHub Discussions or on Discord.
 *    - If permission is granted by the owner, you may copy the software under the terms specified in this license agreement.
 *    - You are not allowed to permit others to copy the source code that you were allowed to copy by the owner.
 *    - Modified or copied code must not be further copied.
 * 2. **  Contributor Attribution  **
 *    - You must attribute the contributors by creating a visible list within the application, showing who originally wrote the source code.
 *    - If you copy or modify this software under owner permission, you must provide links to the profiles of all contributors who contributed to this software.
 * 3. **  Modification Documentation  **
 *    - All modifications made to the software must be documented and listed.
 *    - the owner may incorporate the modifications made by you to enhance this software.
 * 4. **  Consistent Licensing  **
 *    - All copied or modified files must contain the same license text at the top of the files.
 * 5. **  Permission Reversal  **
 *    - If you are granted permission by the owner to copy this software, it can be revoked by the owner at any time. You will be notified at least one week in advance of any such reversal.
 *    - In case of Permission Reversal, if you fail to acknowledge the notification sent by us, it will not be our responsibility.
 * 6. **  License Updates  **
 *    - The license may be updated at any time. Users are required to accept and comply with any changes to the license.
 *    - In such circumstances, you will be given 7 days to ensure that your software complies with the updated license.
 *    - We will not notify you about license changes; you need to monitor the GitHub repository yourself (You can enable notifications or watch the repository to stay informed about such changes).
 * By using this software, you acknowledge and agree to the terms and conditions outlined in this license agreement. If you do not agree with these terms, you are not permitted to use, copy, modify, or distribute this software.
 *
 * Copyright Â© 2024 Dev Kumar
 */

plugins {
    id "com.android.application"
    id "de.undercouch.download"
}

def KEYSTOREPASSWORD = System.getenv('KEYSTOREPASSWORD')
def firebase = System.getenv('Firebase')

if (firebase != null) {
	if (!new File("$projectDir/google-services.json").exists()) {
       new File("$projectDir/google-services.json").text = firebase;
    }
	apply plugin: "com.google.gms.google-services"
} else {
    if (file("google-services.json").exists()) {
        apply plugin: "com.google.gms.google-services"
    }
}

def localPropsFile = rootProject.file("local.properties")
def properties = new Properties()
def signingEnabled = false
def password = ""

if (localPropsFile.exists()) {
    localPropsFile.withInputStream { inputStream ->
        properties.load(inputStream)
    }
}

if (KEYSTOREPASSWORD != null) {
    password = KEYSTOREPASSWORD
    signingEnabled = true
} else if (localPropsFile.exists()) {
    if (properties.getProperty('keyStorePassword') != null) {
        password = properties.getProperty('keyStorePassword')
        signingEnabled = true
    }
}

android {
    namespace 'com.icst.android.appstudio'
    compileSdk 34
    flavorDimensions = ["arch"]
    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getCommitSha = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "HEAD"
            standardOutput = stdout
        }
        return "\"" + stdout.toString().trim() + "\""
    }

    defaultConfig {
        applicationId "com.icst.android.appstudio"
        minSdk 26
        targetSdk 28
        versionCode 1
        versionName "1.0 Alpha01-Snapshot-" + getCommitHash()
        buildConfigField "String", "commitSha", getCommitSha()
        buildConfigField "boolean", "isDeveloperMode", properties.getProperty("isDeveloperMode", "false")
		buildConfigField "java.io.File", "STORAGE", "new java.io.File(\"" + properties.getProperty("STORAGE", "NOT_PROVIDED") + "\")"

        vectorDrawables { 
            useSupportLibrary true
        }
    }

   productFlavors {
        arm {
            dimension "arch"
            versionNameSuffix "-armeabi-v7a"
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86_64", "x86"
            }
        }

        aarch64 {
            dimension "arch"
            versionNameSuffix "-arm64-v8a"
            ndk {
                abiFilters "arm64-v8a", "x86_64", "x86"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    signingConfigs {
        release {
            if (signingEnabled) {
                storeFile file("keystore.jks")
                storePassword password
                keyAlias "AndroidAppStudio"
                keyPassword password
            }
        }

        debug {
            if (signingEnabled) {
                storeFile file("keystore.jks")
                storePassword password
                keyAlias "AndroidAppStudio"
                keyPassword password
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'xsd/catalog.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/NOTICE.md'
    }
}

task downloadBootstrapArm {
    doLast {
        println "Downloading bootstrap for arm..."
        if (file("src/arm/assets/bootstrap.zip").exists()) {
            println "Downloading bootstrap for arm will be ignored as it already exists."
        }
		download.run {
            src "https://github.com/Innovative-CST/termux-packages/releases/download/bootstrap-2024.08.04-r1%2Bapt-android-7/bootstrap-arm.zip"
            dest file("src/arm/assets/bootstrap.zip")
            overwrite false
	    }
	}
}

task downloadBootstrapAarch64 {
    doLast {
        println "Downloading bootstrap for aarch64..."
        if (file("src/aarch64/assets/bootstrap.zip").exists()) {
            println "Downloading bootstrap for aarch64 will be ignored as it already exists."
        }
		download.run {
            src "https://github.com/Innovative-CST/termux-packages/releases/download/bootstrap-2024.08.04-r1%2Bapt-android-7/bootstrap-aarch64.zip"
            dest file("src/aarch64/assets/bootstrap.zip")
            overwrite false
	    }
	}
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs_nio:2.0.2")

    implementation("androidx.appcompat:appcompat:$appcompat_version")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")

    implementation("com.google.android.material:material:$material_design_version")
    implementation("com.github.bumptech.glide:glide:4.12.0")
    implementation("com.google.code.gson:gson:$gson_version")
    implementation("com.squareup.okhttp3:okhttp:3.9.1")
	implementation("com.blankj:utilcodex:blankj_utilcodex_version")

    def editorGroupId = "io.github.Rosemoe.sora-editor"
    implementation platform("$editorGroupId:bom:$sora_editor_version")
    implementation("$editorGroupId:editor")
    implementation("$editorGroupId:language-textmate")

    implementation platform('com.google.firebase:firebase-bom:32.2.3')
    implementation("com.google.firebase:firebase-analytics")
    implementation("com.google.firebase:firebase-database")

    implementation("com.termux.termux-app:terminal-view:0.117")
    implementation("com.termux.termux-app:terminal-emulator:0.117")

    runtimeOnly('com.android.tools:sdklib:31.5.1') {
        exclude group: 'com.sun.activation', module: 'javax.activation'
    }

	implementation project(":core")
    implementation project(":block")
	implementation project(":editor")
	implementation project(":vieweditor")
    implementation project(":prdownloader")
	implementation project(":themeEngine")
}

project.afterEvaluate {
    tasks.named('assembleArmDebug').configure {
        dependsOn downloadBootstrapArm
    }

    tasks.named('assembleAarch64Debug').configure {
        dependsOn downloadBootstrapAarch64
    }
}
